def recordDeployment(owner, repo, ref, status, environmentURL, environment = "preview", description = "Deploy to preview environment") {
  withCredentials([usernamePassword(credentialsId: 'github-app-infra', usernameVariable: 'GITHUB_APP', passwordVariable: 'GH_TOKEN')]) {
    def json = writeJSON(returnText: true, json: [
        "ref": ref,
        "environment": environment,
        "description": description,
        "required_contexts": [],
        "auto_merge": false,
        "auto_inactive": false,
        "transient_environment": false,
    ])
    def id = readJSON(text: sh(script: "gh api repos/${owner}/${repo}/deployments  -X POST --input - << EOF\n${json}\nEOF", returnStdout: true).trim()).id
    if (id == ''){
      error('Unable to create deployment')
    }
    json = writeJSON(returnText: true, json: [
        "state": status,
        "environment": environment,
        "description": description,
        "log_url": "${BUILD_URL}console",
        "environment_url": environmentURL,
        "log_url": "${BUILD_URL}console",
    ])
    sh("gh api repos/${owner}/${repo}/deployments/${id}/statuses  -X POST --input - << EOF\n${json}\nEOF")
  }
}

pipeline {
  agent {
    kubernetes {
      yaml '''
apiVersion: "v1"
kind: "Pod"
metadata:
  labels:
    jenkins: "agent"
    job: "jenkins-io"
spec:
  tolerations:
    - key: "os"
      operator: "Equal"
      value: "linux"
      effect: "NoSchedule"
  affinity:
    nodeAffinity:
      requiredDuringSchedulingIgnoredDuringExecution:
        nodeSelectorTerms:
          - matchExpressions:
              - key: kubernetes.io/os
                operator: In
                values:
                  - linux
  restartPolicy: "Never"
  automountServiceAccountToken: false
  containers:
    - name: "jnlp"
      image: "jenkinsciinfra/builder:2.0.3"
      resources:
        limits: {}
        requests:
          memory: "4Gi"
          cpu: "2"
      '''
    }
  }

  environment {
    TZ = "UTC"
    USE_LOCAL_NODE = "true"
    USE_LOCAL_RUBY = "true"
  }

  triggers {
    cron("${env.BRANCH_NAME == 'master' ? 'H/30 * * * *' : ''}")
  }

  options {
    timeout(time: 60, unit: 'MINUTES')
    ansiColor('xterm')
    disableConcurrentBuilds(abortPrevious: true)
    buildDiscarder logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '5', numToKeepStr: '5')
  }

  stages {
    stage('NPM Install') {
      steps {
        sh 'npm install'
      }
    }

    stage('Bundle Install') {
      steps {
        // throw errors if Gemfile has been modified since Gemfile.lock
        sh '''
          bundle config --global frozen 1
          bundle install
          '''
      }
    }

    stage('Build') {
      steps {
        sh 'make'
      }
    }

    stage('Deploy to preview site') {
      when {
        changeRequest target: 'master'
      }
      environment {
        NETLIFY_AUTH_TOKEN = credentials('netlify-auth-token')
      }
      post {
        success {
          recordDeployment('jenkins-infra', 'jenkins.io', pullRequest.head, 'success', "https://deploy-preview-${CHANGE_ID}--jenkins-io-site-pr.netlify.app", "preview-${CHANGE_ID}")
        }
        failure {
          recordDeployment('jenkins-infra', 'jenkins.io', pullRequest.head, 'failure', "https://deploy-preview-${CHANGE_ID}--jenkins-io-site-pr.netlify.app", "preview-${CHANGE_ID}")
        }
      }
      steps {
        script {
          def netlifySite = readJSON(text: sh(script: 'npx netlify sites:list --json', returnStdout: true).trim()).find { it.name == "jenkins-io-site-pr" }
          env.NETLIFY_SITE_ID = netlifySite['site_id']
          env.NETLIFY_DOMAIN = netlifySite['default_domain']

          sh('npx netlify deploy --message "Preview deploy for ${CHANGE_ID}" --alias "deploy-preview-${CHANGE_ID}" -d build/_site')
        }
      }
    }
  }
}
